/*------------------------------------------------------------------------------
  Plugins DSL block
  https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
------------------------------------------------------------------------------*/
plugins {
    id 'java-library'         // See: https://docs.gradle.org/current/userguide/java_library_plugin.html
}

/*------------------------------------------------------------------------------
  Local sub-project properties
------------------------------------------------------------------------------*/
description = 'The Nyx main library and entry point.'

/*------------------------------------------------------------------------------
  Configurations
*/
configurations {
  // Create new configurations used by other projects to use classes from this project.
  // In particular we need the Git scripts (in the test source set) to be available for other projects.
  // This is actually not trivial and we had to follow directions from this post:
  // - https://softnoise.wordpress.com/2014/09/07/gradle-sub-project-test-dependencies-in-multi-project-builds/
  integrationTestExport.extendsFrom(integrationTestRuntimeOnly)
  functionalTestExport.extendsFrom(functionalTestRuntimeOnly)
}

/*------------------------------------------------------------------------------
  Repositories and dependencies
  https://docs.gradle.org/current/userguide/dependency_management_for_java_projects.html
*/
dependencies {
  // Import dependencies from other modules of this project
  api                   project(':modules:java:version')
  implementation        'org.slf4j:slf4j-api:2.0.2'                                                // SLF4J API (http://www.slf4j.org/)
  implementation        'org.eclipse.jgit:org.eclipse.jgit:6.4.0.202211300538-r'                   // JGit (https://www.eclipse.org/jgit/)
  implementation        'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:6.4.0.202211300538-r'          // JGit JSch extension (https://www.eclipse.org/jgit/)
  implementation        'com.jcraft:jsch:0.1.55'                                                   // JSch SSH2 Java library (http://www.jcraft.com/jsch/)
  implementation        'com.fasterxml.jackson.core:jackson-databind:2.13.4'                       // Jackson JSON library (https://github.com/FasterXML/jackson)
  implementation        'com.fasterxml.jackson.core:jackson-annotations:2.13.4'                    // Jackson JSON library (https://github.com/FasterXML/jackson)
  implementation        'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.4'          // Jackson YAML library (https://github.com/FasterXML/jackson-dataformats-text/tree/master/yaml)
  implementation        'com.github.jknack:handlebars:4.3.0'                                       // Handlebars library (https://github.com/jknack/handlebars.java)
  implementation        'org.kohsuke:github-api:1.313'                                             // GitHub API client Java library (https://github-api.kohsuke.org/)

  // The SLF4J framework that prints all test output to System err
  // See http://www.slf4j.org/apidocs/org/slf4j/impl/SimpleLogger.html for the system properties available to change verbosity levels
  testImplementation            'org.slf4j:slf4j-simple:2.0.2'
  integrationTestImplementation 'org.slf4j:slf4j-simple:2.0.2'

  // See the 'integrationTestExport' and 'functionalTestExport' about this
  integrationTestExport  sourceSets.integrationTest.output
  functionalTestExport   sourceSets.functionalTest.output
}

test {
  // Pass the name of the configuration file examples to tests.
  // We use the same files used for the documentation here.
  systemProperty 'extendedJSONExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-extended.json").absolutePath
  systemProperty 'mediumJSONExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-medium.json").absolutePath
  systemProperty 'simpleJSONExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-simple.json").absolutePath
  systemProperty 'simplestJSONExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-simplest.json").absolutePath
  systemProperty 'extendedYAMLExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-extended.yaml").absolutePath
  systemProperty 'mediumYAMLExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-medium.yaml").absolutePath
  systemProperty 'simpleYAMLExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-simple.yaml").absolutePath
  systemProperty 'simplestYAMLExampleConfigurationFile', file("$rootDir/docs/_includes/.nyx-simplest.yaml").absolutePath
}

integrationTest {
  // Pass the credentials to test suites as per https://github.com/mooltiverse/nyx/blob/master/CONTRIBUTING.md#contributing-code
  // Suggestion is to set the $GRADLE_USER_HOME environment variable and create a gradle.properties therein, with these
  // properties defined
  systemProperty 'gitHubTestUserToken', findProperty("gitHubTestUserToken")
  systemProperty 'gitHubTestUserPublicKey', findProperty("gitHubTestUserPublicKey")
  systemProperty 'gitHubTestUserPrivateKeyPassphrase', findProperty("gitHubTestUserPrivateKeyPassphrase")
  systemProperty 'gitHubTestUserPrivateKeyWithPassphrase', findProperty("gitHubTestUserPrivateKeyWithPassphrase")
  systemProperty 'gitHubTestUserPrivateKeyWithoutPassphrase', findProperty("gitHubTestUserPrivateKeyWithoutPassphrase")
  systemProperty 'gitLabTestUserToken', findProperty("gitLabTestUserToken")
  systemProperty 'gitLabTestUserPublicKey', findProperty("gitLabTestUserPublicKey")
  systemProperty 'gitLabTestUserPrivateKeyPassphrase', findProperty("gitLabTestUserPrivateKeyPassphrase")
  systemProperty 'gitLabTestUserPrivateKeyWithPassphrase', findProperty("gitLabTestUserPrivateKeyWithPassphrase")
  systemProperty 'gitLabTestUserPrivateKeyWithoutPassphrase', findProperty("gitLabTestUserPrivateKeyWithoutPassphrase")
}

functionalTest {
  // Pass the credentials to test suites as per https://github.com/mooltiverse/nyx/blob/master/CONTRIBUTING.md#contributing-code
  // Suggestion is to set the $GRADLE_USER_HOME environment variable and create a gradle.properties therein, with these
  // properties defined
  systemProperty 'gitHubTestUserToken', findProperty("gitHubTestUserToken")
  systemProperty 'gitHubTestUserPublicKey', findProperty("gitHubTestUserPublicKey")
  systemProperty 'gitHubTestUserPrivateKeyPassphrase', findProperty("gitHubTestUserPrivateKeyPassphrase")
  systemProperty 'gitHubTestUserPrivateKeyWithPassphrase', findProperty("gitHubTestUserPrivateKeyWithPassphrase")
  systemProperty 'gitHubTestUserPrivateKeyWithoutPassphrase', findProperty("gitHubTestUserPrivateKeyWithoutPassphrase")
  systemProperty 'gitLabTestUserToken', findProperty("gitLabTestUserToken")
  systemProperty 'gitLabTestUserPublicKey', findProperty("gitLabTestUserPublicKey")
  systemProperty 'gitLabTestUserPrivateKeyPassphrase', findProperty("gitLabTestUserPrivateKeyPassphrase")
  systemProperty 'gitLabTestUserPrivateKeyWithPassphrase', findProperty("gitLabTestUserPrivateKeyWithPassphrase")
  systemProperty 'gitLabTestUserPrivateKeyWithoutPassphrase', findProperty("gitLabTestUserPrivateKeyWithoutPassphrase")
  
  // Pass the 'quickTests' property (if declared by user, i.e. running ''./gradlew -PquickTests=true') to reduce the number of tests
  systemProperty 'quickTests', findProperty("quickTests")
}

/*------------------------------------------------------------------------------
  Additional task dependencies
------------------------------------------------------------------------------*/
