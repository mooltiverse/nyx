/*------------------------------------------------------------------------------
  Plugins DSL block
  https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
------------------------------------------------------------------------------*/
plugins {
  id 'java-gradle-plugin'                         // See: https://docs.gradle.org/current/userguide/java_gradle_plugin.html
  id 'com.gradle.plugin-publish' version '2.0.0'  // See: https://guides.gradle.org/publishing-plugins-to-gradle-plugin-portal/ and https://plugins.gradle.org/docs/publish-plugin
  id 'com.gradleup.shadow' version '9.3.0'        // See: https://github.com/GradleUp/shadow
}

/*------------------------------------------------------------------------------
  Local sub-project properties
------------------------------------------------------------------------------*/
description = 'The Nyx Gradle plugin'

/*------------------------------------------------------------------------------
  Configurations
------------------------------------------------------------------------------*/
configurations {
  // this shouldn't be required but without this the plugin metadata is not available in the functional test classpath
  functionalTestRuntimeOnly.extendsFrom testRuntimeClasspath

  // Include the 'shadow' configuration in the 'compileOnly' (see the dependencies for more).
  // ***CHECK***
  //compileOnly.extendsFrom(shadow)
}

shadowJar {
  // Configure the "shadow" plugin to publish a single, self-contained "fat" JAR
  // including all dependencies required to run the plugin.
  archiveClassifier.set('')
  // This replaces the default jar with the fat jar
  // ***CHECK***
  //configurations = [project.configurations.shadow]
  // Merge service files (common in plugins)
  mergeServiceFiles()
}

/*------------------------------------------------------------------------------
  Dependencies
  https://docs.gradle.org/current/userguide/dependency_management_for_java_projects.html
------------------------------------------------------------------------------*/
dependencies {
  // Import dependencies from other modules of this project
  implementation project(':src:java:main')

  // Import dependencies from other modules of this project. Using the "shadow" configuration
  // they will be included in the final plugin JAR produced by this subproject but they
  // won't be listed as transitive dependencies to the POM file.
  // This configuration is used to create a "fat" (also called "uber") JAR including all
  // dependencies required to run the plugin so we don't have to mess with external maven
  // repositories, making the plugin jar self-contained.
  // ***CHECK***
  /*shadow project(':src:java:main')
  shadow project(':src:java:version')

  compileOnly project(':src:java:main')
  compileOnly project(':src:java:version')*/

  // Add this dependency to use the Git scripts from the main subproject into this project tests
  integrationTestImplementation project(path: ':src:java:main', configuration: 'integrationTestExport')
  functionalTestImplementation  project(path: ':src:java:main', configuration: 'integrationTestExport')
  functionalTestImplementation  project(path: ':src:java:main', configuration: 'functionalTestExport')
}


/*------------------------------------------------------------------------------
  Extension configurations
------------------------------------------------------------------------------*/
// Additional test sets created using the 'jvm-test-suite'
// These tests are better isolated from unit tests (in the src/test directory of each sub project)
// as they may take longer.
testing {
  suites {
    // This is the built-in suite
    test {
      // Here we can configure additional dependencies, properties and options for these tests
    }

    // Integration tests can be implemented in the directory src/integrationTest/[java|groovy]
    integrationTest(JvmTestSuite) {
      // Here we can configure additional dependencies, properties and options for these tests
    }

    // Functional tests can be implemented in the directory src/functionalTest/[java|groovy]
    functionalTest(JvmTestSuite) {
      // Here we can configure additional dependencies, properties and options for these tests
    }
  }
}

gradlePlugin {
  website = 'https://github.com/mooltiverse/nyx'
  vcsUrl = 'https://github.com/mooltiverse/nyx.git'

  plugins {
    nyxPlugin {
      id = rootProject.group
      version = rootProject.version
      displayName = 'Nyx Gradle plugin'
      description = project.description
      implementationClass = 'com.mooltiverse.oss.nyx.gradle.NyxPlugin'
      tags.set([ 'auto-versioning', 'changelog', 'continuous delivery', 'git', 'github', 'gitlab', 'release', 'semantic', 'semantic-release', 'semantic-version', 'semver', 'version', 'versioning' ])
    }
  }
}

/*------------------------------------------------------------------------------
  Publications
------------------------------------------------------------------------------*/
// Override the default publication created by the parent project for proper
// publication of the Gradle plugin.
publishing {
  publications {
    maven(MavenPublication) {
      // This forces the artifact ID to be 'nyx' instead of 'main' or 'gradle'
      artifactId = rootProject.name
      // ***CHECK***
      //from components.java
      // Use the "shadow" plugin artifact which is a "fat" JAR including all dependencies
      from components.shadow

      pom {
        name = rootProject.name
        description = "$rootProject.group:$rootProject.name:$rootProject.version"
        url = projectURL
        licenses {
          license {
            name = projectLicenseName
            url = projectLicenseURL
          }
        }
        developers {
          developer {
            id = projectID
            name = projectTeamName
            email = projectTeamEmail
            organization = projectOrganizationName
            organizationUrl = projectOrganizationURL
          }
        }
        scm {
          connection = projectGitURL
          developerConnection = projectGitURL
          url = projectURL
        }
      }
      // ***CHECK***
      /*pom.withXml {
        // This finds the 'dependencies' node and removes it
        asNode().children().removeIf { it.name().toString().endsWith('dependencies') }
      }*/
      /*pom.withXml {
        // Make sure to remove internal project dependencies from the generated POM file,
        // provided that all dependencies from other modules are bundled into the final JAR
        // thanks to the "shadow" plugin.
        def dependenciesNode = asNode().getAt('dependencies')[0]
        if (dependenciesNode) {
          dependenciesNode.children().removeIf { node ->
            // Remove specific internal projects by their artifactId or groupId
            node.artifactId.text() == 'main' || node.artifactId.text() == 'version' || node.groupId.text() == rootProject.group
          }
        }
      }*/
    }
  }
}

// See https://github.com/gradle/gradle/issues/1246
task setupPublishPluginCredentials() {
  group = 'Publishing'
  description = 'Sets the system properties from environment variables gradlePublishKey and gradlePublishSecret to allow the Gradle Plugin Publish to authenticate'

  doLast {
    def key = findProperty("gradlePublishKey")
    def secret = findProperty("gradlePublishSecret")

    if( !key || !secret)
    {
      logger.warn("Warning: gradlePublishKey and/or gradlePublishSecret are not defined environment variables. This might be ok if not running the publishing tasks but if you need to publish the Gradle Plugin this prevents the publication.")
    }

    System.properties.setProperty("gradle.publish.key", key)
    System.properties.setProperty("gradle.publish.secret", secret)
  }
}

/*------------------------------------------------------------------------------
  Additional task dependencies
------------------------------------------------------------------------------*/
// Disables the standard jar task to avoid confusion with the "shadow" plugin tasks
// ***CHECK***
//tasks.jar.enabled = false
tasks.assemble.dependsOn(shadowJar)

tasks.publishPlugins.dependsOn tasks.setupPublishPluginCredentials
tasks.publish.dependsOn tasks.publishPlugins
// Make sure the global Java artifacts are published before the plugin
tasks.publish.dependsOn tasks.getByPath(':src:java:publish')

// to enforce the fail-fast principle, make sure the main library tests are executed first
tasks.test.dependsOn project(':src:java:main').tasks.test

afterEvaluate {
  // The tasks listed here are those not caught in the section above
  tasks.publish.onlyIf { rootProject.nyxState.newRelease }                    // this actually belongs to the 'publishing' group but doesn't get captured by the above section for some reason
  tasks.publishPlugins.onlyIf { rootProject.nyxState.newRelease }             // from the 'plugin portal' group
}
