/*------------------------------------------------------------------------------
  Plugins DSL block
  https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
------------------------------------------------------------------------------*/
plugins {
  id 'base'                 // See: https://docs.gradle.org/current/userguide/base_plugin.html
  id 'java'                 // See: https://docs.gradle.org/current/userguide/java_plugin.html
  id 'java-library'         // See: https://docs.gradle.org/current/userguide/java_library_plugin.html
  id 'maven-publish'        // See: https://docs.gradle.org/current/userguide/publishing_maven.html
  id 'java-gradle-plugin'   // See: https://docs.gradle.org/current/userguide/java_gradle_plugin.html
  id 'jacoco'               // See: https://docs.gradle.org/current/userguide/jacoco_plugin.html

  //id 'checkstyle'           // See: https://docs.gradle.org/current/userguide/checkstyle_plugin.html
  //id 'pmd'                  // See: https://docs.gradle.org/current/userguide/pmd_plugin.html

  // See: https://github.com/palantir/gradle-docker
  id 'com.palantir.docker' version '0.25.0'

  // Builds a dependency tree of Gradle tasks, i.e. 'gradle build taskTree'
  // This plugin can be commented out and used only when needed.
  // See: https://github.com/dorongold/gradle-task-tree
  id "com.dorongold.task-tree" version "1.5"
}

/*------------------------------------------------------------------------------
  Project overall attributes
------------------------------------------------------------------------------*/
group   = 'com.mooltiverse.oss.nyx'
version = '0.0.1'

/*------------------------------------------------------------------------------
  Repositories and dependencies
  https://docs.gradle.org/current/userguide/dependency_management_for_java_projects.html
------------------------------------------------------------------------------*/
repositories {
  mavenCentral()
}

dependencies {
  api 'org.eclipse.jgit:org.eclipse.jgit:5.6.0.201912101111-r'

  implementation 'net.sourceforge.argparse4j:argparse4j:0.8.1'
  implementation 'org.jetbrains:annotations:15.0'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
}

/*------------------------------------------------------------------------------
  Extension configurations
------------------------------------------------------------------------------*/
compileJava {
  //options.warnings      = false
  //options.deprecation   = false
  options.compilerArgs  += ["-Xlint:unchecked"]
}
compileTestJava {
  //options.warnings      = false
  //options.deprecation   = false
  options.compilerArgs  += ["-Xlint:unchecked"]
}

javadoc {
  options {
    // Link generated javadocs to external docs
    links 'https://docs.oracle.com/en/java/javase/13/docs/api/'
    links 'https://download.eclipse.org/jgit/site/5.6.0.201912101111-r/apidocs/'
    links 'https://argparse4j.github.io/apidocs/'
    links 'https://junit.org/junit5/docs/current/api/'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

docker {
  name project.name
  dockerfile project.file('src/main/docker/Dockerfile')
}

publishing {
  publications {
    nyx(MavenPublication) {
      from components.java
    }
  }

  repositories {
    mavenCentral()
  }
}

/*------------------------------------------------------------------------------
  Custom tasks
------------------------------------------------------------------------------*/

/*------------------------------------------------------------------------------
  Additional task dependencies
------------------------------------------------------------------------------*/
tasks.build.dependsOn tasks.docker
tasks.test.finalizedBy tasks.jacocoTestReport